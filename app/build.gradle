apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'//kapt 插件
apply from: "${rootDir}/common.gradle"
android {
    namespace "com.wrbug.developerhelper"
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    if (isSignFileValid()) {
        signingConfigs {
            releaseConfig
        }
    }
    compileSdk 34
    defaultConfig {
        applicationId "com.wrbug.developerhelper"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 100040
        versionName "1.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "arm64-v8a"
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
            if (isSignFileValid()) {
                signingConfig signingConfigs.releaseConfig
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.google.code.gson:gson:2.10'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.github.yhaolpz:FloatWindow:1.0.9'
    implementation project(':basecommon')
    kapt "com.android.databinding:compiler:3.1.4"
    implementation 'com.elvishew:xlog:1.6.1'
    implementation 'org.jetbrains.anko:anko-commons:0.10.8'
    //dagger2
    implementation 'com.google.dagger:dagger:2.16'
    // https://mvnrepository.com/artifact/dom4j/dom4j
    implementation 'dom4j:dom4j:1.6.1'
    implementation 'com.evrencoskun.library:tableview:0.8.8'
    implementation 'gdut.bsx:share2:0.9.3'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    implementation 'de.blox:graphview:0.5.0'
    implementation project(':basemoduleimport')
    implementation project(":commonwidget")
    implementation project(':ipc')
}
kapt {
    generateStubs = true
}


Properties props = new Properties()
def propFile = file('../signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
    if (props != null &&
            props.containsKey('SIGN_FILE') && props.containsKey('SIGN_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.releaseConfig.storeFile = file(props['SIGN_FILE'])
        android.signingConfigs.releaseConfig.storePassword = props['SIGN_PASSWORD']
        android.signingConfigs.releaseConfig.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.releaseConfig.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'release build not found signing properties'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'release build not found signing file'
    android.buildTypes.release.signingConfig = null
}

def isSignFileValid() {
    return file('../signing.properties').canRead()
}
